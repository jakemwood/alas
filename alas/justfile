set dotenv-load

cargo := if os() == "linux" { "cargo" } else { "cross" }
target := if os() == "linux" { "" } else { "--target aarch64-unknown-linux-gnu --release" }

[working-directory: 'frontend']
@build-frontend:
    npm run build

[working-directory: 'frontend']
@deploy-frontend:
    rsync -r dist/ ${PI_USERNAME}@${PI_HOSTNAME}:/home/${PI_USERNAME}/static

check:
    {{ cargo }} check {{ target }}

build *args:
    {{ cargo }} build {{ target }} {{ args }}

deploy:
    scp target/aarch64-unknown-linux-gnu/release/alas ${PI_USERNAME}@${PI_HOSTNAME}:/home/${PI_USERNAME}

test *args:
    {{ cargo }} test {{ target }} {{ args }}
